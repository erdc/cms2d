! *****************************************************************
      SUBROUTINE SHEARLUND(IRIPPLE,DEP,UC,UW,T,PHI,RHOS,RHOW,D50,  &
                           TAUCT,TAUWT,TAUWMT,TAUCWT,TAUCWMT,      &
                           TAUCTB,TAUCWTB,TAUCWMTB,FCF,FWF,FCWF)
! CALCULATION OF TOTAL SHEAR STRESS TO BE USED WITH THE LUND FORMULA
!
! ROUGHNESS ESTIMATED WITH REGARD TO EFFECTS OF GRAIN SIZE, BED FORMS,
! AND SEDIMENT TRANSPORT
!
! A VALUE ON IRIPPLE DIFFERENT FROM 1 WILL DISABLE RIPPLE ROUGHNESS
! CALCULATION
!
! CODED BY M. LARSON
! 2005-11-01
!******************************************************************
      use const_def, only: pi
      use flow_def, only: grav
      use prec_def
      implicit none
      integer :: iripple
      real(ikind), parameter:: kappa=0.4
      real(ikind) :: DEP,UC,UW,T,PHI,RHOS,RHOW,D50, &
               TAUCT,TAUWT,TAUWMT,TAUCWT,TAUCWMT, &
               TAUCTB,TAUCWTB,TAUCWMTB,FCF,FWF,FCWF,&
               AW,PW,RIPWH,RIPWL,RHRWL,RIPCH,RIPCL,RHRCL,&
               RKWF,RKCF,RKS,XI,PSI,RKTW,RKTC,&
               Z0S,Z0FW,Z0FC,Z0TW,Z0TC,Z0W,Z0C,Z0CB,Z0WB,&
               FCFB,FCWFB,RRR,RRRB,FWFB,X,TAUWTB,TAUWMTB
      

! IRIPPLE=0 : DO NOT INCLUDE RIPPLES
! IRIPPLE=1 : INCLUDE RIPPLES

! BOTTOM EXCURSION AMPLITUDE
      IF(UW.GT.0.0)THEN 
        AW=UW*T/(2.0*PI)
      ELSE
        AW=0.0
      ENDIF
      IF(IRIPPLE.EQ.1)THEN

! BED FORMS AND ROUGHNESS
! ***********************
! WAVES
! -----
! VAN RIJN'S EQUATION FOR RIPPLE PROPERTIES
! (UNDER IRREGULAR WAVES)
!
! ESTIMATE VALUE ON NON-DIMENSIONAL PARAMETER PSI
        IF(UW.GT.0.0)THEN
          PW=UW**2/((RHOS/RHOW-1.0)*GRAV*D50)
          IF(PW.LE.10.D0)THEN
            RIPWH=0.22*AW
            RIPWL=1.25*AW
            RHRWL=RIPWH/RIPWL
          ELSEIF(PW.LT.250.0)THEN                  !Chris Reed - Changed from .LE. to .LT. - 10/18/2016
            RIPWH=2.8e-13 *(250.0-PW)**5.0*AW
            RIPWL=1.4e-6 *(250.0-PW)**2.5*AW
            RHRWL=RIPWH/RIPWL
          ELSE
            RIPWH=0.D0
            RIPWL=0.D0
            RHRWL=0.D0
          ENDIF

! ROUGHNESS FROM WAVE RIPPLES (RKWF)
          RKWF=30.0*0.25*RIPWH*RHRWL
        ELSE
          RKWF=0.0
        ENDIF

! CURRENT
! -------
        IF(UC.GT.0.0)THEN
          RIPCL=1000.0*D50
          RIPCH=RIPCL/7.0
          RHRCL=RIPCH/RIPCL

! ROUGHNESS FROM CURRENT RIPPLES (RKCF)
          RKCF=30.0*0.25*RIPCH*RHRCL
        ELSE
          RKCF=0.0
        ENDIF
      ELSE
        RKCF=0.0
        RKWF=0.0
      ENDIF

! ROUGHNESS RELATED TO GRAIN SIZE AND SEDIMENT TRANSPORT
! ******************************************************
! ROUGHNESS RELATED TO GRAIN FRICTION (RKS)
      RKS=2.0*D50

! ROUGHNESS RELATED TO SEDIMENT TRANSPORT
!
! ROUGHNESS DETERMINED BY SOLVING THE WILSON AND SWART FORMULA
! SIMULTANEOUSLY - NUMERICALLY DETERMINED SOLUTION IN NON-DIMENSIONAL
! TERMS APPROXIMATED BY POLYNOMIALS (FLAT BED)
!

! WAVES (RKTW)
      IF(UW > 0.0 .AND. T > 0.0)THEN
        XI=LOG(UW/(RHOS/RHOW-1.0)/GRAV/T)
        IF(XI < -5.0)THEN
          PSI=0.58184 + 1.62956*XI + 0.03004*XI**2
        ELSEIF(XI > -5.0 .AND. XI < -3.0)THEN
          PSI=3.23319 + 2.70983*XI + 0.14110*XI**2
        ELSE
          PSI=51.59618 + 56.02861*XI + 19.85308*XI**2 + 2.43875*XI**3
        ENDIF
        RKTW=EXP(PSI)*AW
      ELSE
        RKTW=0.d0
      ENDIF

! CURRENT (RKTC)
!
! ROUGHNESS DETERMINED BY SOLVING THE WILSON FORMULA ASSUMING A LOG
! PROFILE FOR THE CURRENT VELOCITY - NUMERICALLY DETERMINED SOLUTION 
! IN NON-DIMENSIONAL TERMS APPROXIMATED BY POLYNOMIALS (FLAT BED)
      IF(UC > 0.001 .and. dep >= 0.001)THEN
        XI=LOG(UC**2 / (RHOS/RHOW-1.0) / GRAV / DEP)
        IF(XI < -4.0)THEN
          PSI=-4.167248 + 1.269405*XI + 0.0083*XI**2
        ELSEIF(XI >= -4.0 .AND. XI < -1.0)THEN
          PSI=-3.958030 + 1.376399*XI + 0.022333*XI**2
        ELSE
          PSI=-3.907731 + 1.461098*XI + 0.086438*XI**2 + 0.028461*XI**3
        ENDIF
        RKTC=EXP(PSI)*DEP
      ELSE
        RKTC=0.0
      ENDIF

! ROUGHNESS HEIGHTS
      Z0S = RKS/30.0
      Z0FW = RKWF/30.0
      Z0FC = RKCF/30.0
      Z0TW = RKTW/30.0
      Z0TC = RKTC/30.0
      Z0W = Z0S + Z0FW + Z0TW
      Z0C = Z0S + Z0FC + Z0TC

! ROUGHNESS FOR BED LOAD CALCULATIONS
      Z0CB = Z0S + Z0TC
      Z0WB = Z0S + Z0TW

! FRICTION FACTORS
! ****************
! CURRENT FRICTION FACTOR (LOG PROFILE)
      IF(DEP > 0.0)THEN
        FCF  = MIN(2.0 * (KAPPA/(tiny(z0c)  + 1.0 + LOG(Z0C/DEP)))**2, 0.30)   !0.05 cutoff added by C. Reed, MEB added tiny to this value to avoid divide by zero 03/31/2025.
        FCFB = MIN(2.0 * (KAPPA/(tiny(z0cb) + 1.0 + LOG(Z0CB/DEP)))**2, 0.30)  !0.05 cutoff added by C. Reed, MEB added tiny to this value to avoid divide by zero 03/31/2025.
      ELSE
        FCF = 0.0
        FCFB = 0.0
      ENDIF
! WAVE FRICTION FACTOR (SWART)
      IF(AW > 0.0)THEN
        RRR = MAX(AW/(30.0*Z0W),1.0)
        RRRB = MAX(AW/(30.0*Z0WB),1.0)
        FWF = MIN(EXP(-5.98+5.2*RRR**(-0.194)),0.30)
        FWFB = MIN(EXP(-5.98+5.2*RRRB**(-0.194)),0.30)
      ELSE
        FWF = 0.0
        FWFB = 0.0
      ENDIF
! FRICTION WEIGHTING FUNCTION
      IF(ABS(UC).GT.0.0)THEN
        X=ABS(UC)/(ABS(UC)+UW)
      ELSE
        X=0.0
      END IF
! WEIGHTED FRICTION VALUE (WAVE AND CURRENT COMBINED)
      FCWF=X*FCF+(1.0-X)*FWF
      FCWFB=X*FCFB+(1.0-X)*FWFB

! SHEAR STRESSES
! **************
! CURRENT
      TAUCT=0.5*RHOW*FCF*UC**2
      TAUCTB=0.5*RHOW*FCFB*UC**2
! WAVES, MEAN VALUE
      TAUWT=0.25*RHOW*FWF*UW**2
      TAUWTB=0.25*RHOW*FWFB*UW**2
! WAVES, MAXIMUM VALUE
      TAUWMT=0.5*RHOW*FWF*UW**2
      TAUWMTB=0.5*RHOW*FWFB*UW**2
! COMBINED, MEAN VALUE
      TAUCWT=SQRT(TAUCT**2+TAUWT**2+2.0*TAUCT*TAUWT*ABS(COS(PHI)))
      TAUCWTB=SQRT(TAUCTB**2+TAUWTB**2+2.0*TAUCTB*TAUWTB*ABS(COS(PHI)))
! COMBINED, MAXIMUM VALUE
      TAUCWMT=SQRT(TAUCT**2+TAUWMT**2+2.0*TAUCT*TAUWMT*ABS(COS(PHI)))
      TAUCWMTB=SQRT(TAUCTB**2+TAUWMTB**2+2.0*TAUCTB*TAUWMTB*ABS(COS(PHI)))

      RETURN
      END SUBROUTINE !SHEARLUND

! *****************************************************************
      SUBROUTINE SUSPLUND(DEP,UC,UW,RHOS,RHOW,D50,                  &
                          WS,DST,TAUCT,TAUWT,TAUWMT,TAUCWT,TAUCWMT, &
                          FCF,FWF,DB,TAUCR,CRCW,EPSCW,QSS,          &
                          BDpart,USTC,USTW)
!     LUND-CIRP FORMULA FOR PREDICTING SUSPENDED LOAD INCLUDING 
!     REFERENCE CONCENTRATION AND SEDIMENT MIXING COEFFICIENT
!
!     CODED BY M. LARSON
!     2005-11-01
!******************************************************************
      use const_def, only: pi
      use flow_def, only: grav
      use prec_def
      implicit none
      real(ikind),parameter:: kappa=0.4
      real(ikind):: DEP,UC,UW,RHOS,RHOW,D50,         &
           WS,DST,TAUCT,TAUWT,TAUWMT,TAUCWT,TAUCWMT, &
           FCF,FWF,DB,TAUCR,CRCW,EPSCW,QSS,          &
           BDpart,USTC,USTW,USTCR,KC,KB,KW,Y,        &
           SIGMAW,SIGMAC,SIGMAWC,DC,DW,DTOT,THETACR, &
           THETAC,THETAW,THETAWM,THETACWM,THETACW,ACRCW

! SHEAR VELOCITIES
! ****************
! CURRENT SHEAR VELOCITY
      USTC=SQRT(0.5*FCF*UC**2)
! WAVE SHEAR VELOCITY
      USTW=SQRT(0.5*FWF*UW**2)
! SHEAR VELOCITY AT INCIPIENT MOTION
      USTCR=SQRT(TAUCR/RHOW)

! SEDIMENT MIXING COEFFICIENT
! ***************************
! CURRENT ONLY
      IF(USTC.GT.0.0)THEN
         IF(WS/USTC.LE.1.0)THEN
            SIGMAC=0.4+3.5*(SIN(PI/2.0*WS/USTC))**2
         ELSE
            SIGMAC=1.0+2.9*(SIN(PI/2.0*USTC/WS))**2
         ENDIF
      ELSE
         SIGMAC=0.0
      ENDIF
      KC=SIGMAC*KAPPA/6.0
! WAVES ONLY
      IF(USTW.GT.0.0)THEN
         IF(WS/USTW.LE.1.0)THEN
            SIGMAW=0.150+1.500*(SIN(PI/2.0*WS/USTW))**2
         ELSE
            SIGMAW=1.0+0.650*(SIN(PI/2.0*USTW/WS))**2
         ENDIF
      ELSE
         SIGMAW=0.0
      ENDIF
      KW=SIGMAW*KAPPA/9.42
! WAVES AND CURRENT COMBINED
!      IF(TAUCT.GT.0.d0 .OR. TAUWMT.GT.0.d0)THEN
      IF(TAUCT.GT.0.0 .AND. TAUWMT.GT.0.0)THEN     !reported by Alex on 12/05/2008
         Y=TAUCT/(TAUCT+TAUWMT)
         SIGMAWC=Y*SIGMAC+(1.0-Y)*SIGMAW
         KC=SIGMAWC*KAPPA/6.0
         KW=SIGMAWC*KAPPA/9.42
      ENDIF
! WAVE BREAKING COEFFICIENT
      KB=0.01

! ENERGY DISSIPATION RATE
! ***********************
! CURRENT AND NON-BREAKING WAVES
      DC=TAUCT*USTC
      DW=TAUWMT*USTW
! TOTAL WEIGHTED ENERGY DISSIPATION
      DTOT=KB**3*DB+KC**3*DC+KW**3*DW
! SEDIMENT MIXING COEFFICIENT
      EPSCW=(DTOT/RHOW)**(1.0/3.0)*DEP
! AVOID ZERO MIXING
      EPSCW=MAX(EPSCW,1.0e-6)

! REFERENCE CONCENTRATION
! ***********************
! CRITICAL SHIELDS NUMBER FOR INCIPIENT MOTION
      THETACR=TAUCR/((RHOS-RHOW)*GRAV*D50)
! CURRENT SHIELDS NUMBER
      THETAC=TAUCT/((RHOS-RHOW)*GRAV*D50)
! WAVE SHIELDS NUMBER (MEAN)
      THETAW=TAUWT/((RHOS-RHOW)*GRAV*D50)
! WAVE SHIELDS NUMBER (MAXIMUM)
      THETAWM=TAUWMT/((RHOS-RHOW)*GRAV*D50)
! WAVE-CURRENT SHIELDS NUMBER (MEAN)
      THETACW=TAUCWT/((RHOS-RHOW)*GRAV*D50)
! WAVE-CURRENT SHIELDS NUMBER (MAXIMUM)
      THETACWM=TAUCWMT/((RHOS-RHOW)*GRAV*D50)
! COEFFICIENT IN REFERENCE CONCENTRATION EQUATION
      ACRCW=0.0035*EXP(-0.3*DST)
! REFERENCE CONCENTRATION
      IF(THETACWM .GT. 0.2*THETACR)THEN
         CRCW=ACRCW*THETACW*EXP(-4.5*THETACR/THETACWM)
      ELSE
         CRCW=0.0
      ENDIF
! CALCULATE SUSPENDED TRANSPORT RATE
      IF(DEP.GT.0.0 .and. EPSCW.GT.0.0 .and. WS.GT.0.0) then
         BDpart = EPSCW/WS*(1.0-EXP(-WS*DEP/EPSCW))
         QSS=UC*CRCW*BDpart
      ELSE
         QSS = 0.0
      ENDIF

      RETURN
      END SUBROUTINE !SUSPLUND

! *****************************************************************
      SUBROUTINE BEDLUND(RHOS,RHOW,TAUCTB,TAUCWTB,TAUCWMTB,TAUCR,QBS)
!     LUND-CIRP FORMULA FOR PREDICTING BED LOAD TRANSPORT 
!
!     CODED BY M. LARSON
!     2005-11-01
!******************************************************************
      use prec_def
      use flow_def, only: grav 
      implicit none
      real(ikind),parameter:: AC=12.0, BC=4.5 ! LUND-CIRP TRANSPORT MODEL
      real(ikind):: RHOS,RHOW,TAUCTB,TAUCWTB,TAUCWMTB,TAUCR,QBS

! BED LOAD TRANSPORT
      IF(TAUCWMTB .GT. 0.2*TAUCR) THEN
         QBS=AC*SQRT(TAUCTB/RHOW)*TAUCWTB*EXP(-BC*TAUCR/TAUCWMTB)
         QBS=QBS/(RHOS-RHOW)/GRAV
      ELSE
       QBS = 0.0
      ENDIF

      RETURN
      END SUBROUTINE !BEDLUND

! *****************************************************************
      SUBROUTINE CROSSMEAN(DEP,HS,PERIOD,L,BRWRATIO,RHOS,RHOW,D50,WS,&
                          FCF,TAUCR,TAUCWTB,TAUCWMTB,CRCW,EPSCW,&
                          UM,UR,QSM,QSR,QBM)
!     SEDIMENT TRANSPORT BY CROSS-SHORE MEAN FLOW
!     -------------------------------------------
!
!     VERTICAL VELOCITY DISTRIBUTION ASSUMED TO BE CONSTANT, DIRECTED OFFSHORE
!     UNDER TROUGH LEVEL AND ONSHORE ABOVE TROUGH LEVEL. AN EXPONENTIAL
!     CONCENTRATION DISTRIBUTION OVER THE WATER COLUMN IS USED, EXTENDING
!     TO THE CREST LEVEL OF THE WAVES. ESTIMATES OF UNDERTOW VELOCITY BASED
!     ON RATTANAPITIKON AND SHIBAYAMA (2000), WHICH INCLUDES STOKES DRIFT
!     AND CONTRIBUTION FROM THE ROLLER.
!
!     CODED BY M. LARSON
!     2009-11-19
!
!******************************************************************
! INPUT VARIABLES
! ---------------
! H = WAVE HEIGHT
! DEP = WATER DEPTH
! PERIOD = WAVE PERIOD
! L = WAVE LENGTH
! BRWRATIO = RATIO OF BREAKING WAVES
! D50 = MEDIAN GRAIN SIZE
! RHOW = DENSITY OF WATER
! RHOS = DENSITY OF SEDIMENT
! WS = SEDIMENT FALL SPEED
! FCF = CURRENT FRICTION FACTOR
! TAUCR = CRITICAL SHEAR STRESS FOR INCIPIENT MOTION
! TAUCWTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MEAN VALUE (BED LOAD)
! TAUCWMTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MAX VALUE (BED LOAD)
! CRCW = REFERENCE CONCENTRATION
! EPSCW = SEDIMENT DIFFUSIVITY
!
! OUTPUT VARIABLES
! ----------------
! QSM = SUSPENDED TRANSPORT BELOW TROUGH LEVEL 
!       (RETURN FLOW = UNDERTOW; OFFSHORE)
! QSR = SUSPENDED TRANSPORT ABOVE TROUGH LEVEL
!       (IN THE ROLLER; ONSHORE)
! QBM = BED LOAD TRANSPORT
!       (RETURN FLOW = UNDERTOW; OFFSHORE)
! UM = MEAN UNDERTOW VELOCITY BELOW TROUGH LEVEL (OFFSHORE)
! UR = MEAN UNDERTOW VELOCITY ABOVE TROUGH LEVEL (ONSHORE)
      use prec_def
      implicit none
      real(ikind),parameter:: AC=12.0, BC=4.5 ! LUND-CIRP TRANSPORT MODEL
      real(ikind),parameter:: BS=0.08, CS1=0.76, CS2=1.12 ! RATTANAPITIKON AND SHIBAYAMA (2000) (BS-FACTOR FROM SVENDSEN)
      real(ikind),parameter:: G=9.81  !Gravity
      real(ikind):: HS,HRMS,HMEAN,PERIOD,DEP,RHOS,RHOW,D50,BRWRATIO,WS,FCF,TAUCR,VAL,&
            TAUCWTB,TAUCWMTB,CRCW,EPSCW,L,C,DC,DT,UM,UR,QSM,QSR,QBM,TETASTIR            

! DETERMINE WAVE PHASE SPEED
      C=L/PERIOD

! DETERMINE WAVE HEIGHTS
      HRMS = HS/SQRT(2.0)      
      HMEAN = 0.78*DEP
      
! TROUGH AND CREST DEPTH
      DT=DEP-HRMS/2.0
      DC=DEP+HRMS/2.0

! UNDERTOW VELOCITY (DIRECTED OFFSHORE)
      UM=CS1*BS*G*HRMS**2/C/DT + CS2*BS*C*HMEAN/DT*BRWRATIO !Alex, changed H to HRMS and HMEAN
!      UM=CS1*BS*G*HRMS**2/C/DT + 0.0*BS*C*HMEAN/DT*BRWRATIO !Alex, changed H to HRMS and HMEAN
      IF(UM.LT.1.0E-4)THEN !Alex
        UM=0.0;UR=0.0
        QSR=0.0;QBM=0.0
        RETURN
      ENDIF  

! MEAN VELOCITY ABOVE TROUGH LEVEL (DIRECTED ONSHORE)
      UR=UM*DT/HRMS !Alex, changed H to HRMS

! SUSPENDED TRANSPORT BELOW TROUGH LEVEL (OFFSHORE)
      QSM=UM*CRCW*EPSCW/WS*(1.0-EXP(-WS*DT/EPSCW))

! SUSPENDED TRANSPORT ABOVE TROUGH LEVEL (ONSHORE)
      VAL=MIN(WS*HRMS/EPSCW/2.0,10.0)  !Alex, limit to avoid infinite number, changed H to HRMS
      QSR=UR*CRCW*EPSCW/WS*EXP(-WS*DT/EPSCW)*2.0*SINH(VAL)      

! BED LOAD TRANSPORT (OFFSHORE)
      TETASTIR=TAUCWTB/(RHOS-RHOW)/G/D50
      QBM=AC*SQRT(FCF/2.0)*D50*UM*TETASTIR*EXP(-BC*TAUCR/TAUCWMTB)

      RETURN
      END

! *****************************************************************
      SUBROUTINE CROSSMEAN_ROL(DEP,HRMS,PERIOD,L,QR,RHOS,RHOW,D50,WS,&
                          FCF,TAUCR,TAUCWTB,TAUCWMTB,CRCW,EPSCW,&
                          UM,UR,QSM,QSR,QBM)
!     SEDIMENT TRANSPORT BY CROSS-SHORE MEAN FLOW
!     -------------------------------------------
!
!     VERTICAL VELOCITY DISTRIBUTION ASSUMED TO BE CONSTANT, DIRECTED OFFSHORE
!     UNDER TROUGH LEVEL AND ONSHORE ABOVE TROUGH LEVEL. AN EXPONENTIAL
!     CONCENTRATION DISTRIBUTION OVER THE WATER COLUMN IS USED, EXTENDING
!     TO THE CREST LEVEL OF THE WAVES. ESTIMATES OF UNDERTOW VELOCITY BASED
!     ON RATTANAPITIKON AND SHIBAYAMA (2000), WHICH INCLUDES STOKES DRIFT
!     AND CONTRIBUTION FROM THE ROLLER.
!
!     CODED BY M. LARSON
!     2009-11-19
!     MODIFIED BY ALEX SANCHEZ
!     2010-11-30
!       - USES ROLLER FLUX FROM ROLLER MODEL
!
!******************************************************************
! INPUT VARIABLES
! ---------------
! HRMS = ROOT MEAN SQUARED WAVE HEIGHT
! DEP = WATER DEPTH
! QR = SURFACE ROLLER FLUX
! D50 = MEDIAN GRAIN SIZE
! RHOW = DENSITY OF WATER
! RHOS = DENSITY OF SEDIMENT
! WS = SEDIMENT FALL SPEED
! FCF = CURRENT FRICTION FACTOR
! TAUCR = CRITICAL SHEAR STRESS FOR INCIPIENT MOTION
! TAUCWTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MEAN VALUE (BED LOAD)
! TAUCWMTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MAX VALUE (BED LOAD)
! CRCW = REFERENCE CONCENTRATION
! EPSCW = SEDIMENT DIFFUSIVITY
!
! OUTPUT VARIABLES
! ----------------
! QSM = SUSPENDED TRANSPORT BELOW TROUGH LEVEL 
!       (RETURN FLOW = UNDERTOW; OFFSHORE)
! QSR = SUSPENDED TRANSPORT ABOVE TROUGH LEVEL
!       (IN THE ROLLER; ONSHORE)
! QBM = BED LOAD TRANSPORT
!       (RETURN FLOW = UNDERTOW; OFFSHORE)
! UM = MEAN VELOCITY BELOW TROUGH LEVEL (OFFSHORE)
! UR = MEAN VELOCITY ABOVE TROUGH LEVEL (ONSHORE)
      use prec_def
      implicit none
      real(ikind),parameter:: AC=12.0, BC=4.5 ! LUND-CIRP TRANSPORT MODEL
      real(ikind),parameter:: BS=0.08, CS1=0.76, CS2=1.12 ! RATTANAPITIKON AND SHIBAYAMA (2000) (BS-FACTOR FROM SVENDSEN)
      real(ikind),parameter:: G=9.81  !Gravity
      real(ikind):: HRMS,PERIOD,L,C,DEP,RHOS,RHOW,D50,QR,WS,FCF,TAUCR,VAL,&
            TAUCWTB,TAUCWMTB,CRCW,EPSCW,DC,DT,UM,UR,QSM,QSR,QBM,TETASTIR            

! DETERMINE WAVE PHASE SPEED
      C=L/PERIOD
      
! TROUGH AND CREST DEPTH
      DT=DEP-HRMS/2.0
      DC=DEP+HRMS/2.0  
      
! UNDERTOW VELOCITY (DIRECTED OFFSHORE)
!!      UM=CS1*BS*G*HRMS**2/C/DT + CS2*BS*C*HMEAN/DT*BRWRATIO !Alex, changed H to HRMS and HMEAN      
      UM=CS1*BS*G*HRMS**2/C/DT  + CS2*QR/DT !Alex, changed to surface roller flux
      IF(UM.LT.1.0E-4)THEN !Alex
        UM=0.0;UR=0.0
        QSR=0.0;QBM=0.0
        RETURN
      ENDIF  

! MEAN VELOCITY ABOVE TROUGH LEVEL (DIRECTED ONSHORE)
      UR=UM*DT/HRMS !Alex, changed H to HRMS      

! SUSPENDED TRANSPORT BELOW TROUGH LEVEL (OFFSHORE)
      QSM=UM*CRCW*EPSCW/WS*(1.0-EXP(-WS*DT/EPSCW))

! SUSPENDED TRANSPORT ABOVE TROUGH LEVEL (ONSHORE)
      VAL=MIN(WS*HRMS/EPSCW/2.0,10.0)  !Alex, limit to avoid infinite number, changed H to HRMS
      QSR=UR*CRCW*EPSCW/WS*EXP(-WS*DT/EPSCW)*2.0*SINH(VAL)      

! BED LOAD TRANSPORT (OFFSHORE)
      TETASTIR=TAUCWTB/(RHOS-RHOW)/G/D50
      QBM=AC*SQRT(FCF/2.0)*D50*UM*TETASTIR*EXP(-BC*TAUCR/TAUCWMTB)

      RETURN
      END      

! *****************************************************************
      SUBROUTINE BRWRATIO(DEP,HRMS,GAMMA,ALFA)
!
!     ROUTINE TO COMPUTE THE RATIO OF BREAKING WAVES
!     ----------------------------------------------
!
!     THE RATIO OF BREAKING WAVES IS ESTIMATED BASED ON THE RMS WAVE
!     HEIGHT PREDICTED BY CMS-WAVE, THE WATER DEPTH, AND THE BREAKER
!     DEPTH RATIO. THIS RMS WAVE HEIGHT INCLUDES BOTH BREAKING AND
!     NON-BREAKING WAVES. THE RATIO OF BREAKING WAVES IS DETERMINED
!     BY CHOPPING OFF A RAYLEIGH DISTRIBUTION AT A WAVE HEIGHT
!     CORRESPONDING TO INCIPIENT BREAKING. THE SOLUTION TO THE 
!     RESULTING EQUATION IS APPROXIMATED BY A 5TH-ORDER POLYNOMIAL
!     TO ALLOW FOR RAPID CALCULATION.
!
!     CODED BY M. LARSON
!     2009-12-15
!
!******************************************************************
! INPUT VARIABLES
! ---------------
! HRMS = ROOT-MEAN-SQUARE WAVE HEIGHT
! DEP = WATER DEPTH
! GAMMA = BREAKER DEPTH RATIO
!
! OUTPUT VARIABLES
! ----------------
! ALFA = RATIO OF BREAKING WAVES

! VARIABLE DECLARATIONS
      use prec_def
      real(ikind) :: HRMS,DEP,GAMMA,ALFA,D

! NON-DIMENSIONAL VARIABLE
      D=(HRMS/GAMMA/DEP)**2

! RATIO OF BREAKING WAVES
! (CALCULATED USING AN EMPIRICAL FORMULA)
      IF(D.LT.0.130)THEN
         ALFA=0.0
      ELSEIF(D.LT.1.0)THEN
         ALFA=0.028625855130-0.40087872210*D+1.295311650*D**2+&
             0.9925638881D0*D**3-1.420121016D0*D**4+&
             0.5046975858D0*D**5
      ELSE
         ALFA=1.0
      ENDIF
      
      RETURN
      END

!******************************************************************
      SUBROUTINE ASYMMETRY(DEP,H,PERIOD,L,UO,RHOS,RHOW,D50,FWF,TAUCR,&
                          TAUCWTB,TAUCWMTB,QBA)
     
!     SEDIMENT TRANSPORT BY WAVE ASYMMETRY
!     ------------------------------------
!
!     CNOIDAL WAVE THEORY IS EMPLOYED TO ESTIMATE THE ASYMMETRY IN THE
!     HORIZONTAL WAVE VELOCITY VARIATION. THE WAVE PARAMETERS USED
!     TO QUANTIFY THE ASYMMETRY ARE: PEAK POSITIVE AND NEGATIVE
!     VELOCITY AND ASSOCIATED DURATIONS. THE ASYMMETRY FROM CNOIDAL
!     WAVE THEORY IS DESCRIBED IN THE TRANSPORT EQUATION THROUGH
!     ONE PARAMETER, WHICH IS APPROXIMATED WITH EMPIRICAL EXPRESSIONS.
!     THE SEDIMENT TRANSPORT IS COMPUTED USING THE LUND-CIRP BED LOAD
!     FORMULA.
!
!     CODED BY M. LARSON
!     2009-11-24
!
!******************************************************************
!
! INPUT VARIABLES
! ---------------
! H = WAVE HEIGHT
! DEP = WATER DEPTH
! PERIOD = WAVE PERIOD
! L = WAVE LENGTH
! UO = HORIZONTAL WAVE ORBITAL VELOCITY AMPLITUDE
! D50 = MEDIAN GRAIN SIZE
! RHOW = DENSITY OF WATER
! RHOS = DENSITY OF SEDIMENT
! FWF = WAVE FRICTION FACTOR
! TAUCR = CRITICAL SHEAR STRESS FOR INCIPIENT MOTION
! TAUCWTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MEAN VALUE (BED LOAD)
! TAUCWMTB = SHEAR STRESS DUE TO WAVES AND CURRENTS, MAX VALUE (BED LOAD)
!
! OUTPUT VARIABLES
! ----------------
! QBA = BED LOAD TRANSPORT DUE TO ASYMMETRY (ONSHORE)
!
      use prec_def
      use const_def, only: pi
      use flow_def, only: g=>grav
      implicit none
      real(ikind), parameter :: AW=6.0, BW=4.5 ! LUND-CIRP TRANSPORT MODEL
      real(ikind) :: H,PERIOD,DEP,RHOS,RHOW,D50,FWF,TAUCR,TAUCWTB,TAUCWMTB,&
            QBA,UO,L,RFACT,URANGE,FI,U,URSELL,TETASTIR            

! VELOCITY RANGE
! (FACTOR FROM GRASMEIJER USED)
      RFACT=1.0-0.4*H/DEP
      URANGE=2.0*RFACT*UO

! FACTOR THAT QUANTIFIES ASYMMETRY EFFECTS
! (DERIVED BASED ON 1ST-ORDER CNOIDAL WAVE THEORY; FROM ISOBE 1985)
      URSELL=G*H*PERIOD**2/DEP**2
      IF(URSELL.LT.290.0)THEN
         U=URSELL/10.0
         FI=0.049211381960+0.046810837320*U-0.0050567634490*U**2 &
           +0.00028412385460*U**3-7.935127396E-006*U**4 &
           +8.649461289E-008*U**5
      ELSEIF(URSELL.LT.2000.)THEN
         U=URSELL/100.D0
         FI=0.2442656320+0.002322351770*U-0.00099554053890*U**2 &
           +9.043784974E-005*U**3-3.677714843E-006*U**4 &
           +5.688512724E-008*U**5
      ELSE
         FI=0.21
      ENDIF

! BED LOAD TRANSPORT (ONSHORE-DIRECTED)
      TETASTIR=TAUCWTB/(RHOS-RHOW)/G/D50
      QBA=AW*SQRT(FWF/2.0)*D50*URANGE*FI*TETASTIR*EXP(-BW*TAUCR/TAUCWMTB)

      RETURN
      END
